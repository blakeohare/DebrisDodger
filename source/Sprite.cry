import Graphics2D;
import Math;
import Random;

class Sprite {
	
	static field ASTEROID_COLOR = [100, 80, 50];
	static field PARTICLE_COLOR = [180, 180, 180];
	//static field SHIP_COLOR = [255, 255, 255];
	static field BEACON_COLOR = [255, 0, 0];

	field type;
	field x;
	field y;
	field theta;
	field vx;
	field vy;
	field radius;
	field dead;
	field ttl;
	field last_drawn;

	constructor(type, x, y) {
		this.type = type;
		this.x = x + 0.0;
		this.y = y + 0.0;
		this.theta = Random.randomFloat() * TWO_PI;
		this.vx = Random.randomFloat() * 3;
		this.vy = Random.randomFloat() * 3;
		this.radius = 1.0;
		this.dead = false;
		this.ttl = -1;
		this.last_drawn = -1;
		
		switch (type) {
			case 'player':
				this.radius = 10.0;
				this.vx = 0.0;
				this.vy = 0.0;
				break;
			case 'asteroid':
				this.radius = Random.randomFloat() * 200 + 10;
				break;
			case 'beacon':
				this.radius = 8.0;
				break;
			case 'particle':
				this.radius = 2.0;
				this.vx = 0.0;
				this.vy = 0.0;
				break;
			default:
				break;
		}
	}
	
	function update(world) {
		this.x += this.vx;
		this.y += this.vy;
		if (this.ttl-- == 0) {
			this.dead = true;
		}
	}
	
	function render(rc, xoffset, yoffset) {
		x = this.x + xoffset;
		y = this.y + yoffset;
		left = Math.floor(x - this.radius);
		width = Math.floor(this.radius * 2);
		top = Math.floor(y - this.radius);
		height = Math.floor(this.radius * 2);
		right = left + width;
		bottom = top + height;
		if (right < 0 || left > GAME_WIDTH || bottom < 0 || top > GAME_HEIGHT) return;
		
		switch (this.type) {
			case 'particle':
				color = Sprite.PARTICLE_COLOR;
				break;
			case 'asteroid':
				color = Sprite.ASTEROID_COLOR;
				break;
			case 'beacon':
				color = Sprite.BEACON_COLOR;
				break;
			default:
				this.last_drawn = rc;
				a = this.theta;
				ptleft = this.theta - PI * 3 / 4;
				ptright = this.theta + PI * 3 / 4;
				ax = Math.floor(Math.cos(a) * this.radius) + x;
				ay = Math.floor(Math.sin(a) * this.radius) + y;
				bx = Math.floor(Math.cos(ptleft) * this.radius / 2) + x;
				by = Math.floor(Math.sin(ptleft) * this.radius / 2) + y;
				cx = Math.floor(Math.cos(ptright) * this.radius / 2) + x;
				cy = Math.floor(Math.sin(ptright) * this.radius / 2) + y;
				Graphics2D.Draw.triangle(
					ax, ay, bx, by, cx, cy, 255, 255, 255);
				return;
		}
		
		Graphics2D.Draw.ellipse(left, top, width, height, color[0], color[1], color[2]);
	}
}
